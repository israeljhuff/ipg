# Israel's Parser Generator (IPG)
# grammar definition
# Author: Israel Huff
# https://github.com/israeljhuff/ipg

rules                      : ws (comment ws)* rule+;
rule                       : ws id ws rule_mod rule_sep alts rule_end ws (comment ws)*;
rule_mod                   : ("discard" | "inline" | "mergeup")?;
rule_sep           discard : ws ":" ws;
rule_end           discard : ws ";" ws;
ws                 discard : [ \n\r\t]*;
comment             inline : "#" [^\r\n]*;
id                  inline : [A-Za-z][0-9A-Za-z_]*;
alts                       : alt (alts_sep alt)*;
alts_sep           discard : ws "|" ws;
alt                        : elem (ws elem)*;
elem               mergeup : (group | id | ch_class | string) [?*+]?;
group                      : group_open alts group_close;
group_open         discard : "(" ws;
group_close        discard : ws ")";
string                     : string_quote string_chars string_quote;
string_quote       discard : "\"";
string_chars        inline : string_char*;
string_char         inline : [\u0020-\U0010ffff!"!\\] | esc;
ch_class                   : ch_class_open ch_class_neg? ch_class_range* ch_class_close;
ch_class_open      discard : "[";
ch_class_neg               : "^";
ch_class_close     discard : "]";
ch_class_range             : ch_class_range_neg? ch_class_char (ch_class_range_sep ch_class_char)?;
ch_class_range_neg         : "!";
ch_class_range_sep discard : "-";
ch_class_char       inline : [\u0020-\U0010ffff!\\!\]] | esc;
esc                 inline : "\\" ([\!"\-\[\\\]\^abfnrtv] | unicode);
unicode             inline : "u" hex hex hex hex | "U00" hex hex hex hex hex hex;
hex                 inline : [0-9A-Fa-f];
